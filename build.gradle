apply plugin: 'groovy'

buildscript {
    // inspired by https://github.com/Netflix/gradle-template
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

repositories {
    jcenter()
}

apply from: file('gradle/license.gradle')
apply from: file('gradle/idea.gradle')
apply from: file('gradle/publish.gradle')

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        groovy {
            srcDir 'src/integTest/groovy'
        }
        resources {
            srcDir 'src/integTest/resources'
        }
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.google.guava:guava:17.0'
    compile 'com.github.docker-java:docker-java:0.9.1'

    testCompile 'junit:junit-dep:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

task integTest(type: Test) {
    group = 'verification'
    description = 'Runs the integration tests.'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

    reports.html.destination = file("${reporting.baseDir}/integTests")
    reports.junitXml.destination = file("$buildDir/integTest-results")
}

// run integration tests after unit tests if both are scheduled for execution
integTest.mustRunAfter test

check.dependsOn(integTest)

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

/* Workaround for JDK bug (https://bugs.openjdk.java.net/browse/JDK-8051012)
   regression bug in 7u65, fixed in 7u80 and 8u40
 */
compileTestGroovy.groovyOptions.forkOptions.jvmArgs = ['-noverify']
test.jvmArgs '-noverify'
integTest.jvmArgs '-noverify'


artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}
